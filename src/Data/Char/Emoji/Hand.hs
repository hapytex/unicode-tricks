{-# LANGUAGE DeriveDataTypeable, DeriveGeneric, OverloadedStrings, PatternSynonyms, Safe #-}

{-|
Module      : Data.Char.Emoji.Hand
Description : A module that provides Emojis about hands and fingers.
Maintainer  : hapytexeu+gh@gmail.com
Stability   : experimental
Portability : POSIX

Unicode has emoji's for hands. In this module we make it more convenient
to render hand gestures with a specific skin color.
-}

module Data.Char.Emoji.Hand (
    SingleCharHandGesture(
        WavingHand, RaisedBackOfHand, RaisedHand, VulcanSalute, OkHandSign
      , PinchedFingers, PinchingHand, CrossedFingers, LoveYouGesture, SignOfTheHorns, CallMeHand
    ),
    MultiCharHandGesture,
    pattern FingersCrossed, pattern SpockHand, pattern HornsSign
  ) where

import Control.DeepSeq(NFData)

import Data.Char.Core(UnicodeCharacter(toUnicodeChar, fromUnicodeChar), UnicodeText(toUnicodeText, fromUnicodeText))
import Data.Char.Emoji.SkinColor(WithSkinColorModifierUnicodeText)
import Data.Data(Data)
import Data.Hashable(Hashable)

import GHC.Generics(Generic)

import Test.QuickCheck.Arbitrary(Arbitrary(arbitrary), arbitraryBoundedEnum)

-- | A datatype that constructs /hand gestures/ that correspond with a /single/ 'Char'acter.
data SingleCharHandGesture
  = WavingHand  -- ^ A waving hand, this is denoted with üëã.
  | RaisedBackOfHand  -- ^ The raised back of a hand, this is denoted with ü§ö.
  | RaisedHand  -- ^ A raised hand, this is denoted with ‚úã.
  | VulcanSalute  -- ^ The /Vulcan/ salute, this is denoted with üññ.
  | OkHandSign  -- ^ The okay hand sign, this is denoted with üëå.
  | PinchedFingers  -- ^  The /pinched fingers/ gesture, this is denoted with ü§å.
  | PinchingHand  -- ^ The /pinching hand/ gesture, this is denoted with ü§è.
  | CrossedFingers  -- ^ The /crossed fingers/ gesture, this is denoted with ü§û.
  | LoveYouGesture  -- ^ The /love you/ gesture, this is denoted with ü§ü.
  | SignOfTheHorns  -- ^ The sign of the horns, this is denoted with ü§ò.
  | CallMeHand  -- ^ The /call me/ hand sign, this is denoted with ü§ô.
  | MiddleFinger  -- ^ A middle finger pointing up, this is denoted with üñï.
  | ThumbsUp  -- ^ An emoji where the thumb is pointing upwards, this is denoted with üëç.
  | ThumbsDown  -- ^ An emoji where the thumb is pointing downwards, this is denoted with üëé.
  | RaisedFist  -- ^ An emoji where the fist is rased, this is denoted with ‚úä.
  | FistedHand  -- ^ An emoji of a fisted hand, this is denoted with üëä.
  deriving (Bounded, Data, Enum, Eq, Generic, Ord, Read, Show)

instance Arbitrary SingleCharHandGesture where
  arbitrary = arbitraryBoundedEnum

instance Hashable SingleCharHandGesture

instance NFData SingleCharHandGesture

instance UnicodeCharacter SingleCharHandGesture where
  toUnicodeChar WavingHand = '\x1f44b'
  toUnicodeChar RaisedBackOfHand = '\x1f91a'
  toUnicodeChar RaisedHand = '\x270b'
  toUnicodeChar VulcanSalute = '\x1f596'
  toUnicodeChar OkHandSign = '\x1f44c'
  toUnicodeChar PinchedFingers = '\x1f90c'
  toUnicodeChar PinchingHand = '\x1f90f'
  toUnicodeChar CrossedFingers = '\x1f91e'
  toUnicodeChar LoveYouGesture = '\x1f91f'
  toUnicodeChar SignOfTheHorns = '\x1f918'
  toUnicodeChar CallMeHand = '\x1f919'
  toUnicodeChar MiddleFinger = '\x1f595'
  toUnicodeChar ThumbsUp = '\x1f44d'
  toUnicodeChar ThumbsDown = '\x1f44e'
  toUnicodeChar RaisedFist = '\x270a'
  toUnicodeChar FistedHand = '\x1f44a'
  fromUnicodeChar '\x1f44b' = Just WavingHand
  fromUnicodeChar '\x1f91a' = Just RaisedBackOfHand
  fromUnicodeChar '\x270b' = Just RaisedHand
  fromUnicodeChar '\x1f596' = Just VulcanSalute
  fromUnicodeChar '\x1f44c' = Just OkHandSign
  fromUnicodeChar '\x1f90c' = Just PinchedFingers
  fromUnicodeChar '\x1f90f' = Just PinchingHand
  fromUnicodeChar '\x1f91e' = Just CrossedFingers
  fromUnicodeChar '\x1f91f' = Just LoveYouGesture
  fromUnicodeChar '\x1f918' = Just SignOfTheHorns
  fromUnicodeChar '\x1f919' = Just CallMeHand
  fromUnicodeChar '\x1f595' = Just MiddleFinger
  fromUnicodeChar '\x1f44d' = Just ThumbsUp
  fromUnicodeChar '\x1f44e' = Just ThumbsDown
  fromUnicodeChar '\x270a' = Just RaisedFist
  fromUnicodeChar '\x1f44a' = Just FistedHand
  fromUnicodeChar _ = Nothing

instance UnicodeText SingleCharHandGesture

instance WithSkinColorModifierUnicodeText SingleCharHandGesture

-- | Emoji with hands that map on a /sequence/ of characters instead of one character.
data MultiCharHandGesture
  = RaisedHandWithFingersSplayed
  | VictoryHand
  deriving (Bounded, Data, Enum, Eq, Generic, Ord, Read, Show)

instance Arbitrary MultiCharHandGesture where
  arbitrary = arbitraryBoundedEnum

instance Hashable MultiCharHandGesture

instance NFData MultiCharHandGesture

instance UnicodeText MultiCharHandGesture where
  toUnicodeText RaisedHandWithFingersSplayed = "\x1f590\xfe0f"
  toUnicodeText VictoryHand = "\x270c\xfe0f"
  fromUnicodeText "\x1f590\xfe0f" = Just RaisedHandWithFingersSplayed
  fromUnicodeText "\x270c\xfe0f" = Just VictoryHand
  fromUnicodeText _ = Nothing

instance WithSkinColorModifierUnicodeText MultiCharHandGesture

-- | A pattern synonym for 'CrossedFingers'.
pattern FingersCrossed :: SingleCharHandGesture
pattern FingersCrossed = CrossedFingers

-- | A pattern synonym for the 'VulcanSalute'.
pattern SpockHand :: SingleCharHandGesture
pattern SpockHand = VulcanSalute

-- | A pattern synonym for 'SignOfTheHorns'.
pattern HornsSign :: SingleCharHandGesture
pattern HornsSign = SignOfTheHorns
